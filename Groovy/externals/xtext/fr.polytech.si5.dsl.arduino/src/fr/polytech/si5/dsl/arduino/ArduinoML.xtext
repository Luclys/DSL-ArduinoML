grammar fr.polytech.si5.dsl.arduino.ArduinoML with org.eclipse.xtext.common.Terminals

generate arduinoML "http://www.polytech.fr/si5/dsl/arduino/ArduinoML"


App returns App:
	'app' name=ID 'initial' 'state' initial=[State|ID]
	'{'
		'bricks'
		bricks+=Brick
		(bricks+=Brick)* 
		
		'states'
		states+=State
		( states+=State)* 		
	'}';

Brick returns Brick:
	(Actuator | Sensor) name=ID ':' pin=INT;

Actuator returns Actuator:
	{Actuator}
	'Actuator';

Sensor returns Sensor:
	{Sensor}
	'Sensor';
	
State returns State:
	name=ID '{'
		actions+=Action
		(actions+=Action)* 
		transition=Transition
	'}';


Action returns Action:
	actuator=[Actuator|ID] '<=' value=Signal;

Transition returns Transition:
	sensor=[Sensor|ID] 'is' value=Signal '=>' next=[State|ID];

enum Signal returns Signal:
					HIGH = 'HIGH' | LOW = 'LOW';